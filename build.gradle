plugins {
    id 'java'
    id 'com.google.cloud.tools.jib' version '0.9.13'
}

group 'systems.cauldron'
version '1.0-SNAPSHOT'

sourceCompatibility = 11

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {

    //logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2+'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2+'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-jul', version: '2+'

    //web server
    implementation group: 'io.helidon.webserver', name: 'helidon-webserver', version: '1+'
    implementation group: 'io.helidon.config', name: 'helidon-config-yaml', version: '1+'
    implementation('io.helidon.media.jsonp:helidon-media-jsonp-server:1+') {
        exclude group: 'javax.json'
    }

    //json handling
    implementation group: 'org.eclipse', name: 'yasson', version: '1+'

    //graphql engine
    implementation group: 'com.graphql-java', name: 'graphql-java', version: '10+'

    //unit testing
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5+'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5+'

}

compileJava {
    inputs.property("moduleName", 'systems.cauldron.service.graphql')
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jib.to.image = 'amannm/graphql-helidon-service'